" Undo Apple's non-standard changes in /usr/share/vim/vimrc.
unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

" XDG Base Directory support.
" See https://jorenar.com/blog/vim-xdg
if empty($MYVIMRC) | let $MYVIMRC = expand('<sfile>:p') | endif
if empty($XDG_CACHE_HOME) | let $XDG_CACHE_HOME = $HOME."/.cache" | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = $HOME."/.config" | endif
if empty($XDG_DATA_HOME) | let $XDG_DATA_HOME = $HOME."/.local/share" | endif
if empty($XDG_STATE_HOME) | let $XDG_STATE_HOME = $HOME."/.local/state" | endif
if !has('nvim')
  set runtimepath^=$XDG_CONFIG_HOME/vim
  set runtimepath+=$XDG_DATA_HOME/vim
  set runtimepath+=$XDG_CONFIG_HOME/vim/after
  set packpath^=$XDG_DATA_HOME/vim,$XDG_CONFIG_HOME/vim
  set packpath+=$XDG_CONFIG_HOME/vim/after,$XDG_DATA_HOME/vim/after
  set backupdir=$XDG_STATE_HOME/vim/backup | call mkdir(&backupdir, 'p', 0700)
  set directory=$XDG_STATE_HOME/vim/swap | call mkdir(&directory, 'p', 0700)
  set viewdir=$XDG_STATE_HOME/vim/view | call mkdir(&viewdir, 'p', 0700)
  set undodir=$XDG_STATE_HOME/vim/undo | call mkdir(&undodir, 'p', 0700)
  set viminfofile=$XDG_STATE_HOME/vim/viminfo
endif
let g:netrw_home = $XDG_DATA_HOME."/vim"
call mkdir($XDG_DATA_HOME."/vim/spell", 'p', 0700)
call mkdir($XDG_STATE_HOME."/vim", 'p', 0700)

" Install vim-plug if not found.
if empty(glob($XDG_DATA_HOME.'/vim/autoload/plug.vim'))
  silent !curl
  \ -sfLo $XDG_DATA_HOME/vim/autoload/plug.vim --create-dirs
  \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
endif

" Run PlugInstall if there are missing plugins.
let g:plug_home = $XDG_DATA_HOME.'/vim/plugged'
autocmd VimEnter *
\ if len(filter(values(g:plugs), '!isdirectory(v:val.dir)')) |
\   PlugInstall --sync | source $MYVIMRC |
\ endif

" Install plugins.
call plug#begin()
Plug 'github/copilot.vim'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'tpope/vim-fugitive'
Plug 'rust-lang/rust.vim'
call plug#end()
